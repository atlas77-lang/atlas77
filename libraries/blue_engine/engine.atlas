// For now this will just serve as the base for the "cube" example in BlueEngine/examples/shapes/cube.rs
// Later on, this will become the BlueEngine library
// NB: uint32/float32/int32 are not supported in atlas77 yet.
//package blue_engine;

import "std/expected";
import "std/optional";
import "std/io";
import "std/time";
import "std/math";

import "libraries/blue_engine/error";

private extern new_engine() -> Engine;
private extern get_renderer_from_engine(engine: &Engine) -> &Renderer;
private extern get_object_storage_from_engine(engine: &Engine) -> &ObjectStorage;
private extern get_camera_from_engine(engine: &Engine) -> &Camera;
public struct Engine {
    private:
        engine_data: extern_ptr;
        Engine() {}
    public:
        /// Initializes and returns a new Engine instance
        /// Example: let engine = Engine::init();
        fun init() -> Engine {
            let engine = new_engine();
            return engine;
        }
        /// Get a mutable reference to the renderer associated with this engine
        fun renderer(&this) -> &Renderer {
            return get_renderer_from_engine(&this);
        }
        /// Get a mutable reference to the object storage associated with this engine
        fun objects(&this) -> &ObjectStorage {
            return get_object_storage_from_engine(&this);
        }
        /// Get a mutable reference to the camera associated with this engine
        fun camera(&this) -> &Camera {
            return get_camera_from_engine(&this);
        }
}

private extern new_renderer(window_name: string, width: uint64, height: uint64) -> Renderer;
public struct Renderer {
    private:
        renderer_data: extern_ptr;
        Renderer() {}
    public:
        fun init(window_name: string, width: uint64, height: uint64) -> Renderer {
            let renderer = new_renderer(window_name, width, height);
            return renderer;
        }
}

private extern new_object_storage() -> ObjectStorage;
private extern object_storage_insert(
    storage: extern_ptr, 
    name: string, 
    obj: Object
) -> expected<unit, BlueEngineError>;
private extern get_mut_object_from_storage(storage: extern_ptr, name: string) -> optional<&Object>;
private extern get_object_from_storage(storage: extern_ptr, name: string) -> optional<&const Object>;
public struct ObjectStorage {
    private:
        object_storage_data: extern_ptr;
        ObjectStorage() {}
    public:
        fun init() -> ObjectStorage {
            let storage = new_object_storage();
            return storage;
        }
        fun insert(this, name: string, obj: Object) -> expected<unit, BlueEngineError> {
            return object_storage_insert(this.object_storage_data, name, obj);
        }
        fun get(this, name: string) -> optional<&const Object> {
            return get_object_from_storage(this.object_storage_data, name);
        }
        fun get_mut(this, name: string) -> optional<&Object> {
            return get_mut_object_from_storage(this.object_storage_data, name);
        }
}

private extern new_object_settings(camera_effect: string) -> ObjectSettings;
public struct ObjectSettings {
    private:
        object_settings_data: extern_ptr;
        ObjectSettings() {}
    public:
        fun init(camera_effect: string) -> ObjectSettings {
            let settings = new_object_settings(camera_effect);
            return settings;
        }
}

private extern new_vertex(position: [float64], uv: [float64], normal: [float64]) -> extern_ptr;
public struct Vertex {
    private:
        vertex_data: extern_ptr;
    public:
        Vertex(position: [float64], uv: [float64], normal: [float64]) {
            this.vertex_data = new_vertex(position, uv, normal);
        }
}

private extern new_object(
    name: string, 
    vertices: [Vertex], 
    indices: [uint64], 
    settings: ObjectSettings, 
    renderer: &Renderer
) -> Object;
private extern set_object_color(
    obj: extern_ptr, 
    r: float64, 
    g: float64, 
    b: float64, 
    a: float64
);
public struct Object {
    private:
        object_data: extern_ptr;
        Object() {}
    public:
        fun init(
            name: string, 
            vertices: [Vertex], 
            indices: [uint64], 
            settings: ObjectSettings, 
            renderer: &Renderer
        ) -> Object {
            let object = new_object(name, vertices, indices, settings, renderer);
            return object;
        }
        fun set_color(&this, r: float64, g: float64, b: float64, a: float64) {
            set_object_color(this.object_data, r, g, b, a);
        }
}

private extern set_camera_position(
    camera: extern_ptr, 
    x: float64, 
    y: float64, 
    z: float64
);
public struct Camera {
    private:
        camera_data: extern_ptr;
        Camera() {}
    public:
        fun set_camera_position(&this, x: float64, y: float64, z: float64) {
            set_camera_position(this.camera_data, x, y, z);
        }
}


public fun cube(name: string, settings: ObjectSettings, renderer: &Renderer, objects: &ObjectStorage) -> expected<string, BlueEngineError> {
    let obj = Object::init(
        name,
        [
            new Vertex([-1.0, -1.0, 1.0], [0.0, 1.0], [0.0, 0.0, 0.0]),
            new Vertex([1.0, -1.0, 1.0], [1.0, 1.0], [0.0, 0.0, 0.0]),
            new Vertex([1.0, 1.0, 1.0], [1.0, 0.0], [0.0, 0.0, 0.0]),
            new Vertex([-1.0, 1.0, 1.0], [0.0, 0.0], [0.0, 0.0, 0.0]),
            // Back Face
            new Vertex([-1.0, 1.0, -1.0], [1.0, 0.0], [0.0, 0.0, 0.0]),
            new Vertex([1.0, 1.0, -1.0], [0.0, 0.0], [0.0, 0.0, 0.0]),
            new Vertex([1.0, -1.0, -1.0], [0.0, 1.0], [0.0, 0.0, 0.0]),
            new Vertex([-1.0, -1.0, -1.0], [1.0, 1.0], [0.0, 0.0, 0.0]),
            // Right face
            new Vertex([1.0, -1.0, -1.0], [1.0, 1.0], [0.0, 0.0, 0.0]),
            new Vertex([1.0, 1.0, -1.0], [1.0, 0.0], [0.0, 0.0, 0.0]),
            new Vertex([1.0, 1.0, 1.0], [0.0, 0.0], [0.0, 0.0, 0.0]),
            new Vertex([1.0, -1.0, 1.0], [0.0, 1.0], [0.0, 0.0, 0.0]),
            // Left face
            new Vertex([-1.0, -1.0, 1.0], [1.0, 1.0], [0.0, 0.0, 0.0]),
            new Vertex([-1.0, 1.0, 1.0], [1.0, 0.0], [0.0, 0.0, 0.0]),
            new Vertex([-1.0, 1.0, -1.0], [0.0, 0.0], [0.0, 0.0, 0.0]),
            new Vertex([-1.0, -1.0, -1.0], [0.0, 1.0], [0.0, 0.0, 0.0]),
            // Top face
            new Vertex([1.0, 1.0, -1.0], [1.0, 0.0], [0.0, 0.0, 0.0]),
            new Vertex([-1.0, 1.0, -1.0], [0.0, 0.0], [0.0, 0.0, 0.0]),
            new Vertex([-1.0, 1.0, 1.0], [0.0, 1.0], [0.0, 0.0, 0.0]),
            new Vertex([1.0, 1.0, 1.0], [1.0, 1.0], [0.0, 0.0, 0.0]),
            // Bottom face
            new Vertex([1.0, -1.0, 1.0], [1.0, 0.0], [0.0, 0.0, 0.0]),
            new Vertex([-1.0, -1.0, 1.0], [0.0, 0.0], [0.0, 0.0, 0.0]),
            new Vertex([-1.0, -1.0, -1.0], [0.0, 1.0], [0.0, 0.0, 0.0]),
            new Vertex([1.0, -1.0, -1.0], [1.0, 1.0], [0.0, 0.0, 0.0]),
        ],
        [
            0u, 1u, 2u, 2u, 3u, 0u, // top
            4u, 5u, 6u, 6u, 7u, 4u, // bottom
            8u, 9u, 10u, 10u, 11u, 8u, // right
            12u, 13u, 14u, 14u, 15u, 12u, // left
            16u, 17u, 18u, 18u, 19u, 16u, // front
            20u, 21u, 22u, 22u, 23u, 20u, // back
        ],
        settings,
        renderer
    );
    objects.insert(name, obj).expected_value();
    return expected::<string, BlueEngineError>::expect("Cube created successfully");
}

/// Calls the specified update function every frame.
public extern call_update_function(engine: &Engine, update_function_name: string, start: Time);
fun my_update(delta: Time, engine: &Engine, start_time: Time) {
    const radius: float64 = 5.0;
    let camx = sin_f(delta.elapsed(start_time).sec as float64 * 0.5) * radius;
    let camy = sin_f(delta.elapsed(start_time).sec as float64 * 0.5) * radius;
    let camz = cos_f(delta.elapsed(start_time).sec as float64 * 0.5) * radius;
    engine.camera().set_camera_position(camx, camy, camz);
    return;
}

fun main() {
    println("Starting BlueEngine...");
    let engine = Engine::init();
    if cube(
        "cube1", 
        ObjectSettings::init("default"), 
        engine.renderer(), 
        engine.objects()
    ).is_expected() {
        panic("Failed to create cube object");
    }

    engine
        .objects()
        .get_mut("cube1")
        .value()
        .set_color(0.0, 0.0, 1.0, 1.0);
    
    let start_time = Time::now();
    call_update_function(&engine, "my_update", start_time);
}